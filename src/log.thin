class Source {
  name: string;
  contents: string;

  // These are for internal use by the compiler
  next: Source;
  firstToken: Token;
}

class Range {
  source: Source;
  start: int;
  end: int;

  toString(): string {
    return string_slice(this.source.contents, this.start, this.end);
  }

  equals(other: Range): bool {
    return
      this.source == other.source &&
      this.start == other.start &&
      this.end == other.end;
  }

  enclosingLine(): Range {
    var contents = this.source.contents;
    var start = this.start;
    var end = this.start;

    while (start > 0 && string_get(contents, start - 1) != '\n') {
      start = start - 1;
    }

    var length = string_length(contents);
    while (end + 1 < length && string_get(contents, end) != '\n') {
      end = end + 1;
    }

    return createRange(this.source, start, end);
  }

  rangeAtEnd(): Range {
    return createRange(this.source, this.end, this.end);
  }
}

function createRange(source: Source, start: int, end: int): Range {
  assert(start <= end);
  var range = new Range();
  range.source = source;
  range.start = start;
  range.end = end;
  return range;
}

function spanRanges(left: Range, right: Range): Range {
  assert(left.source == right.source);
  assert(left.end <= right.start);
  return createRange(left.source, left.start, right.end);
}

class Diagnostic {
  range: Range;
  message: string;
  next: Diagnostic;
}

class Log {
  first: Diagnostic;
  last: Diagnostic;

  error(range: Range, message: string): void {
    var diagnostic = new Diagnostic();
    diagnostic.range = range;
    diagnostic.message = message;
    this.append(diagnostic);
  }

  append(diagnostic: Diagnostic): void {
    if (this.first == null) this.first = diagnostic;
    else this.last.next = diagnostic;
    this.last = diagnostic;
  }

  toString(): string {
    var result = StringBuilder_new();
    var d = this.first;

    while (d != null) {
      var lineRange = d.range.enclosingLine();
      var column = d.range.start - lineRange.start;
      var line = 0;

      var i = 0;
      while (i < lineRange.start) {
        if (string_get(lineRange.source.contents, i) == '\n') {
          line = line + 1;
        }
        i = i + 1;
      }

      result
        .append(d.range.source.name)
        .appendChar(':')
        .append(string_intToString(line + 1))
        .appendChar(':')
        .append(string_intToString(column + 1))
        .append(": error: ")
        .append(d.message)
        .appendChar('\n')
        .append(lineRange.toString())
        .appendChar('\n');

      i = 0;
      while (i < column) {
        result.appendChar(' ');
        i = i + 1;
      }

      if (d.range.end - d.range.start <= 1) {
        result.appendChar('^');
      } else {
        i = d.range.start;
        while (i < d.range.end && i < lineRange.end) {
          result.appendChar('~');
          i = i + 1;
        }
      }

      result.appendChar('\n');
      d = d.next;
    }

    return result.finish();
  }
}
