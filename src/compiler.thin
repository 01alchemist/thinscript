enum CompileTarget {
  NONE,
  JAVASCRIPT,
  WEBASSEMBLY,
}

class Compiler {
  log: Log;
  global: Node;
  firstSource: Source;
  lastSource: Source;
  target: CompileTarget;
  context: CheckContext;
  wasm: ByteArray;
  js: String;

  initialize(target: CompileTarget): void {
    assert(this.log == null);
    this.log = new Log();
    this.global = new Node();
    this.global.kind = NodeKind.GLOBAL;
    this.target = target;

    if (target == CompileTarget.WEBASSEMBLY) {
      this.addInput("<native>", libraryForWebAssembly());
    }
  }

  addInput(name: string, contents: string): void {
    var source = new Source();
    source.name = name;
    source.contents = contents;

    if (this.firstSource == null) this.firstSource = source;
    else this.lastSource.next = source;
    this.lastSource = source;
  }

  finish(): bool {
    Profiler_begin();

    var source = this.firstSource;
    while (source != null) {
      source.firstToken = tokenize(source, this.log);
      source = source.next;
    }

    Profiler_end(String_new("lexing"));
    Profiler_begin();

    source = this.firstSource;
    while (source != null) {
      if (source.firstToken != null) {
        var file = parse(source.firstToken, this.log);

        if (file != null) {
          while (file.firstChild != null) {
            var child = file.firstChild;
            child.remove();
            this.global.appendChild(child);
          }
        }
      }
      source = source.next;
    }

    Profiler_end(String_new("parsing"));
    Profiler_begin();

    assert(this.context == null);
    this.context = check(this.global, this.log);

    Profiler_end(String_new("checking"));

    if (this.log.first != null) {
      return false;
    }

    Profiler_begin();

    if (this.target == CompileTarget.JAVASCRIPT) {
      this.js = jsEmit(this.global, this.context);
    }

    else if (this.target == CompileTarget.WEBASSEMBLY) {
      this.wasm = new ByteArray();
      wasmEmit(this.global, this.context, this.wasm);
    }

    Profiler_end(String_new("emitting"));

    return true;
  }
}

extern function Compiler_new(target: CompileTarget): Compiler {
  var compiler = new Compiler();
  compiler.initialize(target);
  return compiler;
}

extern function Compiler_addInput(compiler: Compiler, name: string, contents: string): void {
  compiler.addInput(name, contents);
}

extern function Compiler_finish(compiler: Compiler): void {
  compiler.finish();
}

extern function Compiler_wasm(compiler: Compiler): ByteArray {
  return compiler.wasm;
}

extern function Compiler_js(compiler: Compiler): String {
  return compiler.js;
}

extern function Compiler_log(compiler: Compiler): String {
  return compiler.log.toString();
}
