const POINTER_SIZE = 4;

enum ConversionKind {
  IMPLICIT,
  EXPLICIT,
}

class Type {
  symbol: Symbol;

  isClass(): bool {
    return this.symbol.kind == SymbolKind.TYPE_CLASS;
  }

  isEnum(): bool {
    return this.symbol.kind == SymbolKind.TYPE_ENUM;
  }

  isInteger(): bool {
    return (this.symbol.flags & SYMBOL_FLAG_NATIVE_INTEGER) != 0 || this.isEnum();
  }

  isUnsigned(): bool {
    return (this.symbol.flags & SYMBOL_FLAG_IS_UNSIGNED) != 0;
  }

  underlyingType(context: CheckContext): Type {
    return this.isEnum() ? context.intType : this;
  }

  integerBitMask(): uint {
    return ~0 as uint >> (32 - this.symbol.byteSize * 8) as uint;
  }

  isReference(context: CheckContext): bool {
    return this == context.stringType || this.isClass();
  }

  sizeOf(): int {
    return this.isClass() ? POINTER_SIZE : this.symbol.byteSize;
  }

  alignmentOf(): int {
    return this.sizeOf(); // This is true right now
  }

  toString(): String {
    return this.symbol.name;
  }

  findMember(name: String): Symbol {
    var child = this.symbol.node.firstChild;

    while (child != null) {
      assert(child.kind == NodeKind.VARIABLE || child.kind == NodeKind.FUNCTION);

      if (String_equal(child.symbol.name, name)) {
        return child.symbol;
      }

      child = child.nextSibling;
    }

    return null;
  }
}
